archetype (adl_version=1.4; uid=5499256c-07da-31b1-b48d-3f5309fcdaa6)
	openEHR-EHR-CLUSTER.exam-face.v1
specialize
	openEHR-EHR-CLUSTER.exam.v1

concept
	[at0000.1]

language
	original_language = <[ISO_639-1::en]>

description
	original_author = <
		["date"] = <"23/05/2007">
		["name"] = <"Heather Leslie">
		["organisation"] = <"Ocean Informatics">
		["email"] = <"heather.leslie@oceaninformatics.com">
	>
	lifecycle_state = <"AuthorDraft">
	details = <
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"For recording findings on physical examination of the eye.">
			keywords = <"examination","physical","face">
			copyright = <"copyright (c) 2010 openEHR Foundation">
		>
	>
	other_details = <
		["references"] = <"">
		["MD5-CAM-1.0.1"] = <"6FF788014AC0E25EC3E493DCF355A49D">
		["build_uid"] = <"7fbbbe0e-cfdd-31ba-a41f-73047cd4e098">
	>

definition
	CLUSTER[at0000.1] matches {    -- Examination of the face
		items cardinality matches {1..*; unordered} matches {
			CLUSTER[at0004] occurrences matches {0..1} matches {    -- Normal statements
				items cardinality matches {1..*; unordered} matches {
					ELEMENT[at0005] occurrences matches {0..*} matches {    -- Normal statement
						value matches {
							DV_TEXT matches {*}
						}
					}
				}
			}
			ELEMENT[at0002] occurrences matches {0..1} matches {    -- Clinical description
				value matches {
					DV_TEXT matches {*}
				}
			}
			CLUSTER[at0008] occurrences matches {0..1} matches {    -- Findings
				items cardinality matches {1..*; unordered} matches {
					allow_archetype CLUSTER[at0009] occurrences matches {0..1} matches {    -- Detail
						include
							archetype_id/value matches {/openEHR-EHR-CLUSTER\.exam-generic\.v1|openEHR-EHR-CLUSTER\.exam-mouth\.v1|openEHR-EHR-CLUSTER\.exam-nose\.v1|openEHR-EHR-CLUSTER\.cranial_nerves\.v1|openEHR-EHR-CLUSTER\.inspection(-[a-zA-Z0-9_]+)*\.v1/}
					}
				}
			}
			ELEMENT[at0006] occurrences matches {0..*} matches {    -- Image
				value matches {
					DV_MULTIMEDIA matches {
						media_type matches {
							[openEHR::
							426,
							427,
							429]
						}
					}
				}
			}
		}
	}

ontology
	term_definitions = <
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Examination">
					description = <"Basic examination cluster for describing findings on physical examination when there is no named focus of examination.">
				>
				["at0000.1"] = <
					text = <"Examination of the face">
					description = <"Examination cluster for describing findings on physical examination of the face.">
				>
				["at0002"] = <
					text = <"Clinical description">
					description = <"Textural description of the part examined.">
				>
				["at0004"] = <
					text = <"Normal statements">
					description = <"A group of statements about the normality of the examination.">
				>
				["at0005"] = <
					text = <"Normal statement">
					description = <"A specific statement of normality.">
				>
				["at0006"] = <
					text = <"Image">
					description = <"Drawing or image of the area examined.">
				>
				["at0008"] = <
					text = <"Findings">
					description = <"Clinical findings.">
				>
				["at0009"] = <
					text = <"Detail">
					description = <"More focused examination findings.">
				>
			>
		>
	>
