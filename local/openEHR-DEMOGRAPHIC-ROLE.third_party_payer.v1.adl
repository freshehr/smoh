archetype (adl_version=1.4; uid=7575f823-189e-394f-b34f-cfaf166a32fc)
	openEHR-DEMOGRAPHIC-ROLE.third_party_payer.v1

concept
	[at0000]

language
	original_language = <[ISO_639-1::pt-br]>
	translations = <
		["en"] = <
			language = <[ISO_639-1::en]>
			author = <
				["name"] = <"Sergio Miranda Freire">
				["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
				["email"] = <"sergio@lampada.uerj.br">
			>
		>
	>

description
	original_author = <
		["date"] = <"22/05/2009">
		["name"] = <"Sergio Miranda Freire & Rigoleta Dutra Mediano Dias">
		["organisation"] = <"Universidade do Estado do Rio de Janeiro - UERJ">
		["email"] = <"sergio@lampada.uerj.br">
	>
	lifecycle_state = <"draft">
	details = <
		["pt-br"] = <
			language = <[ISO_639-1::pt-br]>
			purpose = <"Representação dos dados de uma Organização quando atua como uma fonte pagadora (SUS, seguradora de planos de saúde, etc)">
			keywords = <"serviço demográfico","fonte pagadora">
			copyright = <"copyright (c) 2010 openEHR Foundation">
			use = <"Usado em serviços de demografia para coletar dados demográficos sobre uma organização que atua como fonte pagadora">
		>
		["en"] = <
			language = <[ISO_639-1::en]>
			purpose = <"Representation of a third-party payer demographic data">
			keywords = <"demographic service","third-party payer">
			copyright = <"copyright (c) 2010 openEHR Foundation">
			use = <"Used in demographic service to collect demographic data about an organisation in the role of a third-party payer">
		>
	>
	other_details = <
		["MD5-CAM-1.0.1"] = <"b4742e4ee978fc61c4a16478d53e6f20">
		["build_uid"] = <"28625d46-a8fd-32fc-a743-35980f9dfdc4">
	>

definition
	ROLE[at0000] matches {    -- Fonte pagadora
		details matches {
			ITEM_TREE[at0001] occurrences matches {0..1} matches {    -- Documentos de Identificação
				items cardinality matches {1..*; unordered; unique} matches {
					allow_archetype CLUSTER[at0010] occurrences matches {1..*} matches {    -- Documento de Identificação
						include
							archetype_id/value matches {/(provider_identifier)[a-zA-Z0-9_-]*\.v1/}
					}
				}
			}
		}
		identities cardinality matches {1..*; ordered} matches {
			allow_archetype PARTY_IDENTITY[at0002] occurrences matches {1..*} matches {    -- Nome
				include
					archetype_id/value matches {/(organisation_name)[a-zA-Z0-9_-]*\.v1/}
			}
		}
	}

ontology
	term_definitions = <
		["pt-br"] = <
			items = <
				["at0000"] = <
					text = <"Fonte pagadora">
					description = <"Organização no papel de uma fonta pagadora">
				>
				["at0001"] = <
					text = <"Documentos de Identificação">
					description = <"Documentos utilizados pela organização quando atua como fonte pagadora">
				>
				["at0002"] = <
					text = <"Nome">
					description = <"Especifica como a fonte pagadora é conhecida">
				>
				["at0010"] = <
					text = <"Documento de Identificação">
					description = <"Documento de identificação utilizado pela organização quando atua como fonte pagadora">
				>
			>
		>
		["en"] = <
			items = <
				["at0000"] = <
					text = <"Third-party payer">
					description = <"Organisation in the role of a third-party payer">
				>
				["at0001"] = <
					text = <"Identifiers">
					description = <"The identifiers used by the organisation in the role of a third-party payer">
				>
				["at0002"] = <
					text = <"Name">
					description = <"Indicates how the third-party payer is known by">
				>
				["at0010"] = <
					text = <"Identifier">
					description = <"The identifier used by the organisation in the role of a third-party payer">
				>
			>
		>
	>
